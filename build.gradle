buildscript {
    repositories {
        jcenter() // Shadow plugin
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
        classpath 'nu.studer:gradle-jooq-plugin:4.1'
    }
}

defaultTasks 'clean', 'build', 'publish'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow' // Shadow jar

    group 'com.alessiodp.oreannouncer'
    version '2.5.2'

    // Info
    ext.pluginName = "OreAnnouncer"
    ext.pluginDescription = "Collects data about mined blocks"
    ext.pluginAuthor = "AlessioDP"
    ext.pluginWebsite = "https://alessiodp.com/"

    // Development
    ext.adpCore = '1.4.1'
    ext.lombok = '1.18.12'
    ext.powermock = '2.0.5'

    // Libraries
    ext.checkerFramework = '3.2.0'
    ext.guava = '21.0'
    ext.jooq = '3.13.1'
    ext.lastLoginAPI = '1.3.5'
    ext.placeholderApi = '2.10.4'
    ext.slf4j = '1.7.25'

    // Misc
    sourceCompatibility = 1.8

    repositories {
        mavenLocal()

        // Maven central
        mavenCentral()
        // CodeMC repository - ADP Core
        maven { url = 'https://repo.codemc.org/repository/maven-public/' }
        // Spigot repository for Spigot & Bukkit
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        // OSS Sonatype repository for Bungeecord
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        // JitPack repo
        maven { url = 'https://jitpack.io' }
        // Sponge repository for configurate
        maven { url = 'https://repo.spongepowered.org/maven' }
        // Byteflux repository for Libby
        maven { url = 'https://repo.byteflux.net/repository/maven-public' }
    }

    dependencies {
        api group: 'com.google.guava', name: 'guava', version: project.ext.guava

        compileOnly group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok

        testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: project.ext.powermock
        testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: project.ext.powermock
    }

    if (it.name != 'oreannouncer-api' && it.name != 'jpa') {
        shadowJar {
            archiveBaseName = project.ext.pluginName
            archiveClassifier = null
            dependencies {
                include(dependency('com.alessiodp.oreannouncer:.*'))
                include(dependency('com.alessiodp.core:.*'))
            }

            relocate 'com.alessiodp.core', 'com.alessiodp.oreannouncer.core'
        }

        publishing {
            publications {
                shadow(MavenPublication) { publication ->
                    project.shadow.component(publication)
                }
            }

            repositories {
                mavenLocal()
            }
        }
    }
}

dependencies {
    implementation project(':oreannouncer-api')
    implementation project(':common')
    implementation project(':bukkit')
    implementation project(':bungeecord')
    implementation project(':jpa')
}

// Disable OreAnnouncer root project jar
jar.enabled = false
artifacts {
    archives shadowJar
}

shadowJar {
    shadowJar {
        archiveBaseName = project.ext.pluginName
        archiveClassifier = null
        dependencies {
            include(dependency('com.alessiodp.oreannouncer:.*'))
            include(dependency('com.alessiodp.core:.*'))
        }

        relocate 'com.alessiodp.core', 'com.alessiodp.oreannouncer.core'

        relocate 'ninja.leaping.configurate', 'com.alessiodp.oreannouncer.libs.configurate'
        relocate 'com.zaxxer.hikari', 'com.alessiodp.oreannouncer.libs.hikari'
        relocate 'org.slf4j', 'com.alessiodp.oreannouncer.libs.slf4j'
        relocate 'org.flywaydb.core', 'com.alessiodp.oreannouncer.libs.flyway'
        relocate 'org.jooq', 'com.alessiodp.oreannouncer.libs.jooq'
        relocate 'org.reflections', 'com.alessiodp.oreannouncer.libs.reflections'
    }
}