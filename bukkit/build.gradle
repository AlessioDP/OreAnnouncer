repositories {
    mavenCentral()

    // DiscordSRV repository
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    // PlaceholderAPI repository
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    api project(':common')
    api group: 'com.alessiodp.core', name: 'bukkit', version: project.ext.adpCore

    compileOnly (group: 'com.discordsrv', name: 'discordsrv', version: project.ext.discordsrv) {
        exclude module: 'MCDiscordReserializer'
        exclude module: 'JDA'
    }
    compileOnly group: 'me.clip', name: 'placeholderapi', version: project.ext.placeholderApi
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE;
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand([
                'pluginVersion': project.version,
                'pluginName': project.ext.pluginName,
                'pluginDescription': project.ext.pluginDescription,
                'pluginAuthor': project.ext.pluginAuthor,
                'pluginWebsite': project.ext.pluginWebsite
        ])
    }
}